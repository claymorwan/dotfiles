# https://github.com/NixOS/nixpkgs/blob/f143438bde4bcba7cb7f156804a8b1c424bd8e0e/pkgs/by-name/sh/shiru/package.nix
{
  lib,
  stdenv,
  pnpm,
  pnpmConfigHook,
  fetchPnpmDeps,
  nodejs,
  fetchFromGitHub,
  python3,
  electron_39,
  copyDesktopItems,
  makeDesktopItem,
  makeBinaryWrapper,
}:
let
  electron = electron_39;
  pname = "shiru";
  version = "6.4.8";

  src = fetchFromGitHub {
    owner = "RockinChaos";
    repo = "shiru";
    tag = "v${version}";
    hash = "sha256-tbIayIAlsLRHOzgc4MCIfGGFkde9m1lyXg26hKHP5c8=";
  };
in
stdenv.mkDerivation {
  inherit pname version src;

  patches = [
    # electron-shutdown-handler is only used on Windows and tries to download
    # files during build
    ./0001-Remove-Windows-specific-dep.patch
  ];

  nativeBuildInputs = [
    nodejs
    pnpm
    pnpmConfigHook
    python3
    makeBinaryWrapper
    copyDesktopItems
  ];

  pnpmDeps = fetchPnpmDeps {
    inherit pname version src;
    prePnpmInstall = ''
      cd electron
    '';
    fetcherVersion = 2;
    hash = "sha256-7nmZTevWbCDf6bgEj22faVVQ8yaflMwBZyMRfX3N2Eg=";
  };

  buildPhase = ''
    runHook preBuild

    cd electron

    cp -r ${electron.dist} electron-dist
    chmod -R u+w electron-dist

    npm run web:build

    ./node_modules/.bin/electron-builder --dir \
      --c.electronDist=electron-dist \
      --c.electronVersion=${electron.version}

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir -p "$out/share/lib/shiru"
    cp -r dist/*-unpacked/{locales,resources{,.pak}} "$out/share/lib/shiru"

    install -Dm644 buildResources/icon.png "$out/share/icons/hicolor/512x512/apps/shiru.png"

    makeWrapper '${electron}/bin/electron' "$out/bin/shiru" \
      --add-flags "$out/share/lib/shiru/resources/app.asar" \
      --prefix LD_LIBRARY_PATH : "${lib.makeLibraryPath [ stdenv.cc.cc.lib ]}" \
      --inherit-argv0

    runHook postInstall
  '';

  desktopItems = [
    (makeDesktopItem {
      name = "shiru";
      exec = "shiru %U";
      icon = "shiru";
      desktopName = "Shiru";
      genericName = "Personal Media Library";
      comment = "Manage your personal media library, organize your collection, and stream your content in real time, no waiting required!";
      categories = [
        "Video"
        "AudioVideo"
      ];
      keywords = [ "Anime" ];
      mimeTypes = [ "x-scheme-handler/shiru" ];
    })
  ];

  meta = with lib; {
    description = "Stream your personal media library in real-time";
    homepage = "https://github.com/RockinChaos/Shiru";
    changelog = "https://github.com/RockinChaos/Shiru/releases/tag/v${version}";
    license = licenses.gpl3Plus;
    maintainers = with maintainers; [
      naomieow
    ];
    platforms = [ "x86_64-linux" ];
    mainProgram = "shiru";
  };
}
